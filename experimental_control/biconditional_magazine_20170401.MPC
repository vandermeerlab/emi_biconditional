\MEDPC script
\Magazine training
\Authors: Emily Irvine

\Total time: Approx 1h04m
\Num trials: 16 trials total
\Intertrial interval: Average 4min (2.5min to 5.5min)
\Measure times (and output with Python): magazine entry, exit, pellet drop.

\test session
\List A = 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1
\List C = 5", 5", 5", 5", 5", 5", 5", 5", 5", 5", 5", 5", 5", 5", 5", 5", 5"

\Magazine training
List A = 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1
List C = 150", 210", 330", 330", 150", 210", 270", 270", 330", 150", 270", 150", 270", 210", 330", 210", 150"

\ A = trial types
\ B = data array
\ C = list for trial length
\ D = num total trials
\ H = comp record of mag entry vs exit
\ J = pause duration between trials
\ L = computer record of mag num
\ M = num mag entries
\ W = iti countdown
\ X = counter in 0.01s
\ Z = idx into data array

dim B = 9975 \ data array

^mag = 6
^pel = 8
^numtrials = 16
^red = 11

\event codes
^MS = 10000 \mag start
^ME = 20000 \mag end
^PD = 30000 \pellet delivery

\Timer
S.S.1,
    s1,
        #start:  ---> s2
    s2,
        0.01": add X ---> sx

\Main state
S.S.2,
    s1,
        #start: ---> s2

    \Initiate trial time based on ITI (J), set trial type (R) and keep track of total trials (D)
    s2,
        0.01": set J = C(K); set W = J/100; set R = A(F); add K; add F; add D ---> s3

    s3,
        0.01": if D > ^numtrials [@true, @false]
                                  @true: ---> s8
                                  @false: ---> s4
    s4,
        J#t: on ^pel; set B(Z) = ^PD+X/100; add Z ---> s5

    s5,
        0.1": off ^pel ---> s6

    s6,
        0.1": on ^pel; set B(Z) = ^PD+X/100; add Z ---> s7

    s7,
        0.1": off ^pel ---> s2

    s8,
        10': ---> stopabortflush

\Count mag entries
S.S.3,
    s1,
        #start: ---> s2
    s2,
        #r^mag: add M ---> sx

\Record mag entries
S.S.4,
    s1,
        0.01": if (L < M) and (H = 0) [@true, @false]
            @true: set B(Z) = ^MS + X/100; add Z; set H = 1; set L = M ---> sx
            @false: if (L = M) and (H = 1) [@2true, @2false]
                @2true: set B(Z) = ^ME + X/100; add Z; set H = 0 ---> sx
                @2false: set L = M ---> sx

\ITI countdown for display
S.S.5,
    s1,
        1": if W > 0 [@reduce, @pass]
            @reduce: set W = W - 1 ---> sx
            @pass: ---> sx

\Display progress
S.S.6,
    s1,
        1": show 1, elps, X/6000, 2, iti, W, 3, Total_trials, D-1 ---> sx

S.S.7,
    s1,
        1": on ^red ---> sx
